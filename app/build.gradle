apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId "cn.bahamut.vessage"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 20
        versionName '1.1.6'
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    lintOptions {
        abortOnError false
    }

    productFlavors {
        qq {}
        wandoujia {}
        qh360{}
        baidu{}
        huawei{}
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-build"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources true
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources true
            buildConfigField "boolean", "LOG_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "vege_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }

        }
    }

}

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'libs' //就是你放aar的目录地址
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile files('libs/commons-codec-1.10-1.jar')
    compile files('libs/aliyun-oss-sdk-android-2.2.0.jar')
    compile files('libs/okhttp-3.2.0.jar')
    compile 'com.squareup.okio:okio:1.6.0'
    compile 'com.squareup.wire:wire-runtime:2.1.2'
    //compile project(':magicfilter')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile project(':PushSDK')
    compile name: 'SMSSDK-2.0.2', ext: 'aar'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.github.satyan:sugar:1.5'
    compile 'com.kaopiz:kprogresshud:1.0.5'
    compile files('libs/libammsdk.jar')
    compile files('libs/MobCommons-2016.0303.1229.jar')
    compile files('libs/MobTools-2016.0303.1229.jar')
}

apply plugin: 'realm-android'