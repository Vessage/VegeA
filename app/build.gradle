apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yy-MM-dd-hh-mm", TimeZone.getTimeZone("UTC"))
}



android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "cn.bahamut.vessage"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 33
        versionName '3.1'
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]

        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
    }
    lintOptions {
        abortOnError false
    }

    productFlavors {
        qq {}
        wandoujia {}
        qh360{}
        baidu{}
        huawei{}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "-build"
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            buildConfigField "boolean", "LOG_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "vege_v${defaultConfig.versionName}.${defaultConfig.versionCode}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }

        }
    }

}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    jcenter()
    flatDir {
        dirs 'libs' //就是你放aar的目录地址
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile files('libs/commons-codec-1.10-1.jar')
    compile files('libs/aliyun-oss-sdk-android-2.3.0.jar')
    compile files('libs/okhttp-3.4.1.jar')
    //compile project(':magicfilter')
    compile project(':PushSDK')
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile name: 'SMSSDK-2.1.1', ext: 'aar'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.makeramen:roundedimageview:2.2.1'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.github.satyan:sugar:1.5'
    compile 'com.kaopiz:kprogresshud:1.0.5'
    compile files('libs/libammsdk.jar')
    compile files('libs/MobCommons-2016.0714.1402.jar')
    compile files('libs/MobTools-2016.0714.1402.jar')
    compile 'com.scottyab:aescrypt:0.0.1'
    compile 'com.github.lsjwzh.RecyclerViewPager:lib:v1.1.1'
    compile 'com.daasuu:animateHorizontalProgressBar:0.2.4'
}

apply plugin: 'realm-android'